ext['signing.keyId'] = ''
ext['signing.password'] = ''
ext['signing.key'] = ''
ext['ossrhUsername'] = ''
ext['ossrhPassword'] = ''
ext['sonatypeStagingProfileId'] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    // 从文件中读取
    Properties properties = new Properties()
    new FileInputStream(secretPropsFile).withCloseable { is -> properties.load(is) }
    properties.each { name, value -> ext[name] = value }
} else {
    // 使用系统环境变量
    ext['signing.keyId'] = System.getenv('SIGNING_KEY_ID')
    ext['signing.password'] = System.getenv('SIGNING_PASSWORD')
    ext['signing.key'] = System.getenv('SIGNING_KEY')
    ext['ossrhUsername'] = System.getenv('OSSRH_USERNAME')
    ext['ossrhPassword'] = System.getenv('OSSRH_PASSWORD')
    ext['sonatypeStagingProfileId'] = System.getenv('SONATYPE_STAGING_PROFILE_ID')
}

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = sonatypeStagingProfileId
            username = ossrhUsername
            password = ossrhPassword

            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}